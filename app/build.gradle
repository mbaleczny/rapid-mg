apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
//apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "pl.mbaleczny.rapid_mg"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        buildTypes.each {
            it.buildConfigField 'String', 'TWITTER_KEY', TWITTER_KEY
            it.buildConfigField 'String', 'TWITTER_SECRET', TWITTER_SECRET
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        mock.java.srcDirs += 'src/mock/kotlin'
        test.java.srcDirs += 'src/mockTest/kotlin'
        prod.java.srcDirs += 'src/prod/kotlin'
    }
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            minSdkVersion 21
        }
        prod {

        }
    }
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release' &&
                variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true);
        }
    }
    lintOptions {
        abortOnError false
    }
}

kapt {
    generateStubs = true
}

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith('test') || !sourceSet.name.startsWith('androidTest')) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

def supportVersion = '25.2.0'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile("com.android.support:design:$supportVersion") {
        exclude module: 'recyclerview-v7'
    }
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'com.google.dagger:dagger:2.8'
    kapt 'com.google.dagger:dagger-compiler:2.8'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile('com.twitter.sdk.android:twitter:2.3.1@aar') {
        transitive = true;
        exclude module: 'picasso'
        exclude module: 'gson'
        exclude module: 'retrofit'
        exclude module: 'okhttp'
        exclude module: 'support-v4'
        exclude module: 'rxjava'
        exclude module: 'converter-gson'
    }

    compile("io.reactivex.rxjava2:rxandroid:2.0.1") {
        exclude module: 'rxjava'
    }
    compile 'io.reactivex.rxjava2:rxjava:2.0.5'

    compile("com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0") {
        exclude module: 'rxjava'
        exclude module: 'okhttp'
    }
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile("com.squareup.retrofit2:converter-gson:2.1.0") {
        exclude module: 'retrofit'
    }
    compile('com.squareup.okhttp3:logging-interceptor:3.3.1') {
        exclude group: 'okhttp'
    }

    compile 'joda-time:joda-time:2.9.7'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.google.code.gson:gson:2.8.0'

    compile "org.jetbrains.anko:anko-sdk15:0.9"
    compile "org.jetbrains.anko:anko-support-v4:0.9"

    testCompile 'junit:junit:4.12'

    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile("com.nhaarman:mockito-kotlin:1.1.0") {
        exclude group: 'kotlin-stdlib'
    }
    testCompile("org.amshove.kluent:kluent:1.14") {
        exclude group: 'junit'
        exclude group: 'mockito-core'
    }

    testCompile 'org.mockito:mockito-core:2.6.3'
    androidTestCompile 'org.mockito:mockito-android:2.6.3'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

//dokka {
//    includes = ['README.md']
//
//    linkMapping {
//        dir = "src/main/kotlin"
//        url = "https://github.com/mbaleczny/rapid-mg/tree/master/app/src/main/kotlin"
//        suffix = "#L"
//    }
//    sourceDirs = files('src/main/kotlin')
//}
